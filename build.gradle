plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.openapi.generator' version '6.2.1'
}

group = 'com.petal'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor 'org.projectlombok:lombok'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.3.Final'

    compileOnly 'org.projectlombok:lombok'
    compileOnly "org.springframework.boot:spring-boot-starter-data-jpa"

    testCompileOnly 'org.assertj:assertj-core:3.11.1'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // openAPI
    implementation 'io.springfox:springfox-boot-starter:3.0.0'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.2'
    implementation "io.swagger.parser.v3:swagger-parser:2.0.20"
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'javax.servlet:javax.servlet-api:4.0.1'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'

    // mapstruct
    implementation("org.mapstruct:mapstruct:1.5.3.Final")

    // spring
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    runtimeOnly 'com.h2database:h2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-test-autoconfigure'

}

openApiGenerate {
    generatorName = "spring"
    inputSpec = "${projectDir}/src/main/resources/static/pokemon-open-api.yml"
    outputDir = "${buildDir}/generated"
    apiPackage = "com.petal.pokemon"
    invokerPackage = "com.petal.pokemon.invoker"
    modelPackage = "com.petal.pokemon.model"
    configOptions = [
            dateLibrary         : "java8",
            skipDefaultInterface: "true",
            interfaceOnly       : "true"
    ]
}

// forces generation of spring controllers on compile, adding them to the sources for compilation
compileJava.dependsOn tasks.openApiGenerate
sourceSets.main.java.srcDir "$buildDir/generated/src/main/java"
sourceSets.main.resources.srcDir "$buildDir/generated/src/main/resources"
springBoot { mainClass = "PokemonApplication" }

test {
    useJUnitPlatform()
}